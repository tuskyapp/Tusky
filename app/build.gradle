apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getGitSha = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short' , 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.keylesspalace.tusky"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 49
        versionName "3.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
        debug { }
    }

    flavorDimensions "color"
    productFlavors {
        blue { }
        green {
            applicationIdSuffix ".test"
            versionNameSuffix "-"+getGitSha()
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    androidExtensions {
        experimental = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext.supportLibraryVersion = '28.0.0'
ext.daggerVersion = '2.17'


// if libraries are changed here, they should also be changed in LicenseActivity
dependencies {
    implementation('com.mikepenz:materialdrawer:6.0.9@aar') {
        transitive = true
    }
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:exifinterface:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'com.github.connyduck:sparkbutton:1.0.1'
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation('com.theartofdev.edmodo:android-image-cropper:2.7.0') {
        exclude group: 'com.android.support'
    }
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // EmojiCompat
    implementation "com.android.support:support-emoji:$supportLibraryVersion"
    implementation "com.android.support:support-emoji-appcompat:$supportLibraryVersion"
    implementation 'de.c1710:filemojicompat:1.0.14'
    // architecture components
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    //room
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    testImplementation 'org.robolectric:robolectric:4.0-alpha-3'
    testImplementation 'org.mockito:mockito-inline:2.21.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    debugImplementation 'im.dino:dbinspector:3.4.1@aar'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:1.0.0-RC2'
    implementation 'com.uber.autodispose:autodispose-ktx:1.0.0-RC2'
}
